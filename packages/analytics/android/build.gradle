/* groovylint-disable DuplicateStringLiteral */
import io.invertase.gradle.common.PackageJson

buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:8.1.2")
    }
  }
}

plugins {
  id "io.invertase.gradle.build" version "1.5"
}

def appProject
if (findProject(':@react-native-firebase_app')) {
  appProject = project(':@react-native-firebase_app')
} else if (findProject(':react-native-firebase_app')) {
  appProject = project(':react-native-firebase_app')
} else {
  throw new GradleException('Could not find the react-native-firebase/app package, have you installed it?')
}
def packageJson = PackageJson.getForProject(project)
def appPackageJson = PackageJson.getForProject(appProject)
def firebaseBomVersion = appPackageJson['sdkVersions']['android']['firebase']
def jsonMinSdk = appPackageJson['sdkVersions']['android']['minSdk']
def jsonTargetSdk = appPackageJson['sdkVersions']['android']['targetSdk']
def jsonCompileSdk = appPackageJson['sdkVersions']['android']['compileSdk']
def coreVersionDetected = appPackageJson['version']
def coreVersionRequired = packageJson['peerDependencies'][appPackageJson['name']]
// Only log after build completed so log warning appears at the end
if (coreVersionDetected != coreVersionRequired) {
  gradle.buildFinished {
    project.logger.warn("ReactNativeFirebase WARNING: NPM package '${packageJson['name']}' depends on '${appPackageJson['name']}' v${coreVersionRequired} but found v${coreVersionDetected}, this might cause build issues or runtime crashes.")
  }
}

project.ext {
  set('react-native', [
    versions: [
      android : [
        minSdk    : jsonMinSdk,
        targetSdk : jsonTargetSdk,
        compileSdk: jsonCompileSdk,
      ],

      firebase: [
        bom: firebaseBomVersion,
      ],
    ],
  ])
}

apply from: file('./../../app/android/firebase-json.gradle')

String collectionDeactivated = 'false'
String adidEnabled = 'true'
String ssaidEnabled = 'true'
String automaticScreenReportingEnabled = 'true'
String analyticsStorageEnabled = 'true'
String adStorageEnabled = 'true'
String adUserDataEnabled = 'true'
String personalizationEnabled = 'true'

// If nothing is defined, data collection defaults to true
String dataCollectionEnabled = 'true'

if (rootProject.ext && rootProject.ext.firebaseJson) {
  /* groovylint-disable-next-line NoDef, VariableTypeRequired */
  def rnfbConfig = rootProject.ext.firebaseJson
  // If the SDK-wide data collection flag is defined and false, our default flips to false
  if (rnfbConfig.isFlagEnabled('app_data_collection_default_enabled', true) == false) {
    dataCollectionEnabled = 'false'
  }
  // If our specific collection flag is defined, it's status takes precedence
  if (rnfbConfig.isDefined('analytics_auto_collection_enabled')) {
    if (rnfbConfig.isFlagEnabled('analytics_auto_collection_enabled', true)) {
      dataCollectionEnabled = 'true'
    } else {
      dataCollectionEnabled = 'false'
    }
  }

  if (rnfbConfig.isFlagEnabled('analytics_collection_deactivated', false) == true) {
    collectionDeactivated = 'true'
  }
  if (rnfbConfig.isFlagEnabled('google_analytics_adid_collection_enabled', true) == false) {
    adidEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('google_analytics_ssaid_collection_enabled', true) == false) {
    ssaidEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('google_analytics_automatic_screen_reporting_enabled', true) == false) {
    automaticScreenReportingEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('analytics_default_allow_analytics_storage', true) == false) {
    analyticsStorageEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('analytics_default_allow_ad_storage', true) == false) {
    adStorageEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('analytics_default_allow_ad_user_data', true) == false) {
    adUserDataEnabled = 'false'
  }
  if (rnfbConfig.isFlagEnabled('analytics_default_allow_ad_personalization_signals', true) == false) {
    personalizationEnabled = 'false'
  }
}

android {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
  if (agpVersion >= 7) {
    namespace 'io.invertase.firebase.analytics'
  }

  defaultConfig {
    multiDexEnabled true
    manifestPlaceholders = [
      firebaseJsonAutoCollectionEnabled: dataCollectionEnabled,
      firebaseJsonCollectionDeactivated: collectionDeactivated,
      firebaseJsonAdidEnabled: adidEnabled,
      firebaseJsonSsaidEnabled: ssaidEnabled,
      firebaseJsonAutomaticScreenReportingEnabled: automaticScreenReportingEnabled,
      firebaseJsonAnalyticsStorageEnabled: analyticsStorageEnabled,
      firebaseJsonAdStorageEnabled: adStorageEnabled,
      firebaseJsonAdUserDataEnabled: adUserDataEnabled,
      firebaseJsonPersonalizationEnabled: personalizationEnabled
    ]
  }

  buildFeatures {
    // AGP 8 no longer builds config by default
    buildConfig true
  }

  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }

  if (agpVersion < 8) {
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_11
      targetCompatibility JavaVersion.VERSION_11
    }
  }

  sourceSets {
    main {
      java.srcDirs = ['src/main/java', 'src/reactnative/java']
    }
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  api appProject
  implementation platform("com.google.firebase:firebase-bom:${ReactNative.ext.getVersion("firebase", "bom")}")
  implementation "com.google.firebase:firebase-analytics"
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
ReactNative.module.applyReactNativeDependency("api")
