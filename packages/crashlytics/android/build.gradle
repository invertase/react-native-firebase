import com.android.Version
import io.invertase.gradle.common.PackageJson

buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:8.4.0")
    }
  }
}

plugins {
  id "io.invertase.gradle.build" version "1.5"
}

def appProject
if (findProject(':@react-native-firebase_app')) {
  appProject = project(':@react-native-firebase_app')
} else if (findProject(':react-native-firebase_app')) {
  appProject = project(':react-native-firebase_app')
} else {
  throw new GradleException('Could not find the react-native-firebase/app package, have you installed it?')
}
def packageJson = PackageJson.getForProject(project)
def appPackageJson = PackageJson.getForProject(appProject)
def firebaseBomVersion = appPackageJson['sdkVersions']['android']['firebase']
def jsonMinSdk = appPackageJson['sdkVersions']['android']['minSdk']
def jsonTargetSdk = appPackageJson['sdkVersions']['android']['targetSdk']
def jsonCompileSdk = appPackageJson['sdkVersions']['android']['compileSdk']
def coreVersionDetected = appPackageJson['version']
def coreVersionRequired = packageJson['peerDependencies'][appPackageJson['name']]
if (coreVersionDetected != coreVersionRequired) {
  project.logger.warn("ReactNativeFirebase WARNING: NPM package '${packageJson['name']}' depends on '${appPackageJson['name']}' v${coreVersionRequired} but found v${coreVersionDetected}, this might cause build issues or runtime crashes.")
}

project.ext {
  set('react-native', [
    versions: [
      android : [
        minSdk    : jsonMinSdk,
        targetSdk : jsonTargetSdk,
        compileSdk: jsonCompileSdk,
      ],

      firebase: [
        bom: firebaseBomVersion,
      ],
    ],
  ])
}

apply from: file('./../../app/android/firebase-json.gradle')

// Default to 'false' for the feature due to memory usage concerns
String crashlyticsNdkEnabled = 'false'
// Default to 'default' for GWP-ASan mode (other options: 'never', 'always')
String crashlyticsGwpAsanMode = 'default'

if (rootProject.ext && rootProject.ext.firebaseJson) {
  def rnfbConfig = rootProject.ext.firebaseJson
  
  // Allow users to opt-in to NDK crash reporting
  if (rnfbConfig.isFlagEnabled('crashlytics_ndk_enabled', false) == true) {
    crashlyticsNdkEnabled = 'true'
  }
  
  // Allow users to configure GWP-ASan mode: 'default', 'never', or 'always'
  crashlyticsGwpAsanMode = rnfbConfig.getStringValue('crashlytics_gwp_asan_mode', 'default')
}

android {
  def agpVersion = Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
  if (agpVersion >= 7) {
    namespace = 'io.invertase.firebase.crashlytics'
  }

  defaultConfig {
    multiDexEnabled = true
    manifestPlaceholders = [
      firebaseJsonCrashlyticsNdkEnabled: crashlyticsNdkEnabled,
      firebaseJsonCrashlyticsGwpAsanMode: crashlyticsGwpAsanMode
    ]
  }

  buildFeatures {
    // AGP 8 no longer builds config by default
    buildConfig = true
  }

  lintOptions {
    disable 'GradleCompatible'
    abortOnError = false
  }
  if (agpVersion < 8) {
    compileOptions {
      sourceCompatibility = JavaVersion.VERSION_11
      targetCompatibility = JavaVersion.VERSION_11
    }
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  api appProject
  implementation platform("com.google.firebase:firebase-bom:${ReactNative.ext.getVersion("firebase", "bom")}")
  implementation "com.google.firebase:firebase-crashlytics"
  implementation "com.google.firebase:firebase-crashlytics-ndk"
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
ReactNative.module.applyReactNativeDependency("api")

// Configure automatic NDK symbol upload for release variants if NDK is enabled
if (rootProject.ext && rootProject.ext.firebaseJson) {
  def rnfbConfig = rootProject.ext.firebaseJson
  
  if (rnfbConfig.isFlagEnabled('crashlytics_ndk_enabled', false) == true) {
    rootProject.afterEvaluate {
      // Find the app project (the one with com.android.application plugin)
      def appProject = rootProject.subprojects.find { 
        it.plugins.hasPlugin('com.android.application') 
      }
      
      if (appProject != null) {
        appProject.afterEvaluate {
          try {
            def android = appProject.extensions.findByName('android')
            
            if (android != null) {
              // Try to configure all release-type variants
              try {
                android.applicationVariants.all { variant ->
                  // Match variants with 'release' in the name (case insensitive)
                  if (variant.name.toLowerCase().contains('release')) {
                    try {
                      // Access or create the firebaseCrashlytics extension for this variant
                      def buildType = android.buildTypes.findByName(variant.buildType.name)
                      if (buildType != null) {
                        buildType.configure {
                          // Check if firebaseCrashlytics extension exists
                          try {
                            firebaseCrashlytics {
                              nativeSymbolUploadEnabled = true
                            }
                            appProject.logger.info("RNFirebase: Enabled Crashlytics NDK symbol upload for build type '${variant.buildType.name}'")
                          } catch (Exception e) {
                            // Extension might not exist if Crashlytics plugin not applied
                            appProject.logger.warn("RNFirebase: Could not configure Crashlytics NDK symbol upload for build type '${variant.buildType.name}'. " +
                              "Ensure the Crashlytics Gradle plugin is applied in your app/build.gradle: ${e.message}")
                          }
                        }
                      }
                    } catch (Exception e) {
                      appProject.logger.warn("RNFirebase: Could not configure variant '${variant.name}': ${e.message}")
                    }
                  }
                }
              } catch (Exception e) {
                // Fallback: just try to configure the 'release' build type directly
                appProject.logger.info("RNFirebase: Using fallback approach for NDK symbol upload configuration")
                try {
                  android.buildTypes.release {
                    firebaseCrashlytics {
                      nativeSymbolUploadEnabled = true
                    }
                  }
                  appProject.logger.info("RNFirebase: Enabled Crashlytics NDK symbol upload for 'release' build type")
                } catch (Exception fallbackError) {
                  appProject.logger.warn("RNFirebase: Could not automatically enable Crashlytics NDK symbol upload. " +
                    "You may need to configure it manually in your app/build.gradle. " +
                    "See: https://firebase.google.com/docs/crashlytics/android/get-started-ndk#set-up-automatic-native-symbols-upload")
                }
              }
            }
          } catch (Exception e) {
            appProject.logger.warn("RNFirebase: Failed to configure automatic Crashlytics NDK symbol upload: ${e.message}")
          }
        }
      }
    }
  }
}
