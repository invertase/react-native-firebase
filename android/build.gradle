buildscript {
  repositories {
    google()
    jcenter()
    maven {
      url 'https://maven.fabric.io/public'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.1'
  }
}

apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_TARGET_SDK_VERSION = 28
def DEFAULT_SUPPORT_LIB_VERSION = "28.0.0"

android {
  compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  productFlavors {
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

rootProject.gradle.buildFinished { buildResult ->
  if (buildResult.getFailure() != null) {
    //noinspection GroovyUnusedCatchParameter
    try {
      String message = buildResult.getFailure().properties.get("reportableCauses").toString()
      if (message.contains("com.android.dex.DexException: Multiple dex files define Lcom/google/") ||
        message.contains("java.util.zip.ZipException: duplicate entry: com/google/android/gms/")) {
        logger.log(LogLevel.ERROR, "")
        logger.log(LogLevel.ERROR, " -----------------------------------------------------------")
        logger.log(LogLevel.ERROR, "|                    REACT NATIVE FIREBASE                  |")
        logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|  This is a common build error when using libraries that   |")
        logger.log(LogLevel.ERROR, "|  require google play services.                            |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|  This error occurs because different versions of google   |")
        logger.log(LogLevel.ERROR, "|  play services or google play services modules are being  |")
        logger.log(LogLevel.ERROR, "|  required by different libraries.                         |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|  A temporary fix would be to set:                         |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|             android { multiDexEnabled true }              |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|  inside your build gradle, however it is recommended for  |")
        logger.log(LogLevel.ERROR, "|  your prod build that you de-duplicate these to minimize  |")
        logger.log(LogLevel.ERROR, "|  bundle size.                                             |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, "|  See http://invertase.link/dupe-dex for how to do this.   |")
        logger.log(LogLevel.ERROR, "|                                                           |")
        logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
      }
    } catch (Exception exception) {
    }
  }
}

repositories {
  google()
  jcenter()

  def found = false
  def parentDir = rootProject.projectDir
  def androidSourcesName = 'React Native sources'
  def androidPrebuiltBinaryName = 'React Native prebuilt binary'

  1.upto(4, {
    if (found) return true
    parentDir = parentDir.parentFile

    // Running React Native from sources locally or for ExpoKit
    def androidSourcesDir = new File(
      parentDir,
      'node_modules/react-native'
    )

    // Official releases of React Native come with a prebuilt version of Android sources
    // in ./android, e.g. react-native/android/**/react-native-0.57.1.aar
    def androidPrebuiltBinaryDir = new File(
      parentDir,
      'node_modules/react-native/android'
    )

    if (androidPrebuiltBinaryDir.exists()) {
      maven {
        url androidPrebuiltBinaryDir.toString()
        name androidPrebuiltBinaryName
      }

      println "${project.name}: using ${androidPrebuiltBinaryName} from ${androidPrebuiltBinaryDir.toString()}"
      found = true
    } else if (androidSourcesDir.exists()) {
      maven {
        url androidSourcesDir.toString()
        name androidSourcesName
      }

      println "${project.name}: using ${androidSourcesName} from ${androidSourcesDir.toString()}"
      found = true
    }
  })

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources or prebuilt binary. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

def supportVersion = rootProject.hasProperty('supportLibVersion') ? rootProject.supportLibVersion : DEFAULT_SUPPORT_LIB_VERSION

dependencies {
  //noinspection GradleDynamicVersion
  api "com.facebook.react:react-native:+"

  /* ----------------------------
   *    REACT NATIVE FIREBASE
   * ---------------------------- */

  // Required dependencies
  //noinspection GradleCompatible
  compileOnly "com.google.firebase:firebase-core:16.0.9"
  compileOnly "com.google.android.gms:play-services-base:16.1.0"

  /* -------------------------
   *   OPTIONAL FIREBASE SDKS
   * ------------------------- */

  // Ads
  compileOnly "com.google.firebase:firebase-ads:17.2.1" 
  // Authentication
  compileOnly "com.google.firebase:firebase-auth:17.0.0"
  // Analytics
  compileOnly "com.google.firebase:firebase-analytics:16.5.0"
  // Performance Monitoring
  compileOnly "com.google.firebase:firebase-perf:17.0.2"
  // Remote Config
  compileOnly "com.google.firebase:firebase-config:17.0.0"
  // Cloud Storage
  compileOnly "com.google.firebase:firebase-storage:17.0.0"
  // Dynamic Links (with invites sub-dependency for internal API)
  compileOnly "com.google.firebase:firebase-dynamic-links:17.0.0"
  compileOnly "com.google.firebase:firebase-invites:17.0.0"
  // Real-time Database
  compileOnly "com.google.firebase:firebase-database:17.0.0"
  // Cloud Functions
  compileOnly "com.google.firebase:firebase-functions:17.0.0"
  // Cloud Firestore
  compileOnly "com.google.firebase:firebase-firestore:19.0.2"
  // Cloud Messaging / FCM
  compileOnly "com.google.firebase:firebase-messaging:18.0.0"
  // Crashlytics
  compileOnly('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
    transitive = true
  }
  /* --------------------------------
   *  OPTIONAL SUPPORT LIBS
   * -------------------------------- */

  // For React Native Firebase Notifications
  api "com.android.support:support-v4:$supportVersion"

  // For React Native Firebase Notifications
  compileOnly 'me.leolin:ShortcutBadger:1.1.22@aar'
}
