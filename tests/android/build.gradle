buildscript {
  ext.kotlinVersion = '1.3.20'
  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://maven.fabric.io/public'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.1'
    classpath 'com.google.gms:google-services:4.2.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.google.firebase:firebase-plugins:1.1.5'
    classpath 'io.fabric.tools:gradle:1.26.1'
  }
}

allprojects {
  task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
    doLast {
      configurations.findAll().each { config ->
        if (config.name.contains("minReactNative") && config.canBeResolved) {
          print config.name
          print '\n'
          config.files
        }
      }
    }
  }

  println "${project.name} ${projectDir} ${rootDir}"

  repositories {
    google()
    jcenter()
    mavenLocal()
    maven {
      url "$rootDir/../node_modules/react-native/android"
    }
  }

}

subprojects {
  task listAllDependencies(type: DependencyReportTask) {}
  ext {
    compileSdk = 28
    buildTools = "28.0.3"
    minSdk = 18
    targetSdk = 28
  }

  afterEvaluate { project ->
    if (!project.name.equalsIgnoreCase("app")
      && project.hasProperty("android")) {
      android {
        compileSdkVersion compileSdk
        buildToolsVersion buildTools
        defaultConfig {
          minSdkVersion minSdk
          targetSdkVersion targetSdk
        }
        variantFilter { variant ->
          def names = variant.flavors*.name
          if (names.contains("minReactNative44")) {
            setIgnore(true)
          }
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.10.2'
  distributionUrl = distributionUrl.replace("bin", "all")
}

