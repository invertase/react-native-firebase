buildscript {
  ext.buildToolsVersion = "34.0.0"
  ext.minSdkVersion = 23
  ext.compileSdkVersion = 34
  ext.targetSdkVersion = 34

  ext.ndkVersion = "26.1.10909125"
 
  ext.kotlinVersion = '2.0.0' // https://kotlinlang.org/releases.html
  ext.supportLibVersion = '1.13.1' // this maps to androidx.core https://developer.android.com/jetpack/androidx/releases/core
  ext.appCompatVersion = '1.7.0' // this maps to androidx.appcompat https://developer.android.com/jetpack/androidx/releases/appcompat
  ext.supportVersion = ext.supportLibVersion
  ext.frescoVersion = '3.2.0' // https://github.com/facebook/fresco/releases
  ext.fragmentVersion = '1.8.1' // https://developer.android.com/jetpack/androidx/releases/fragment
  ext.vectordrawableVersion = '1.2.0' // https://developer.android.com/jetpack/androidx/releases/vectordrawable
  ext.androidxAnnotationVersion = '1.8.0' // https://developer.android.com/jetpack/androidx/releases/annotation
  ext.googlePlayServicesLocationVersion = '21.3.0' // https://developers.google.com/android/guides/setup
  ext.googlePlayServicesVersion = '18.5.0' // play-services-base
  ext.googlePlayServicesAuthVersion = '21.2.0' // play-services-auth
  ext.googlePlayServicesVisionVersion = '20.1.3' // play-services-vision
  ext.googlePlayServicesIidVersion = '17.0.0' // play-services-iid - deprecated, device-info only
  ext.mediaCompatVersion = '1.7.0' // https://developer.android.com/jetpack/androidx/releases/media
  ext.supportV4Version = '1.0.0' // https://developer.android.com/jetpack/androidx/releases/legacy androidx.legacy:legacy-support-v4
  ext.swiperefreshlayoutVersion = '1.1.0' // https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout

  // used to aggregate all modules for later reporting
  ext.firebaseModulePaths = []

  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.gms:google-services:4.4.2' // https://developers.google.com/android/guides/google-services-plugin
    classpath 'com.android.tools.build:gradle:8.5.0' // https://mvnrepository.com/artifact/com.android.tools.build/gradle?repo=google
    classpath("com.facebook.react:react-native-gradle-plugin")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    classpath 'com.google.firebase:perf-plugin:1.4.2'
    classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
    classpath 'com.google.firebase:firebase-appdistribution-gradle:5.0.0'
  }
}

// collect all our modules for reporting
subprojects.forEach { project ->
  if (project.name.contains('react-native-firebase')) {
    rootProject.ext.firebaseModulePaths << project.projectDir
  }
}

subprojects {
  task listAllDependencies(type: DependencyReportTask) { }

  // Force all subprojects to use one and only one set of versions
  ext {
    compileSdk = rootProject.ext.compileSdkVersion
    minSdk = rootProject.ext.minSdkVersion
    targetSdk = rootProject.ext.targetSdkVersion
  }
  afterEvaluate { project ->
    if (!project.name.equalsIgnoreCase('app')
      && project.hasProperty('android')) {
      android {
        compileSdkVersion compileSdk
        defaultConfig {
          minSdkVersion minSdk
          targetSdkVersion targetSdk
        }
      }
    }

    // Configure testing settings - if set in module build.gradle files they affect library consumers
    if (project.name.contains('react-native-firebase')
      && project.hasProperty('android')) {
      android {
        // Instrument all our modules for coverage generation
        buildTypes.debug.testCoverageEnabled true
        jacoco.version '0.8.12'
        testOptions.unitTests.includeAndroidResources = true

        // Make sure our project are run through basic lint checks
        lintOptions {
          abortOnError = true
          warningsAsErrors false
          lintConfig file('./lint.xml')
          ignore 'UnknownNullness', 'SyntheticAccessor', 'LogConditional'
          checkReleaseBuilds = true
          checkAllWarnings true
          showAll true
          textReport true
          textOutput 'stdout'
          htmlReport true
        }
      }
    }
  }
}

apply plugin: "com.facebook.react.rootproject"